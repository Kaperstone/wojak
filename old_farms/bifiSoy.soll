// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

/*

    The architecture of Soyfarms are hard to grasp.

    @issues: When a user deposits on each epoch passing, he is not letting revenue to build up
             But what happens to his previous investment? its lost.

*/

import "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import "@openzeppelin/contracts/access/AccessControlEnumerable.sol";
import "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";

import "./../IPancakeswap.sol";

contract BifiSoy is ERC20, AccessControlEnumerable {
    using SafeERC20 for IERC20;
    using SafeERC20 for IStaking;

    uint8 private constant _decimals = 18;

    event Deposit(uint soyAmount);
    event Withdraw(uint tokenAmount);

    bytes32 private constant CONTRACT_ROLE = keccak256("CONTRACT_ROLE");
    bytes32 private constant FARMERS = keccak256("FARMERS");

    IERC20 public wjk = IERC20(address(0)); // When buying new wjk
    IStaking public zwjk = IStaking(address(0)); // We transfer new wjk to swjk

    ILending public constant LTOKEN = ILending(0x0467c22fB5aF07eBb14C851C75bFf4180674Ed64); // The token from the lending platform we interact with
    IERC20 public constant ITOKEN = IERC20(0xd6070ae98b8069de6B494332d1A1a81B6179D960); // The token we are investing with, like BIFI
    IUniswapV2Router02 public constant SWAP_ROUTER = IUniswapV2Router02(0xF491e7B69E4244ad4002BC14e878a34207E38c29);
    IERC20 public constant SCREAM = IERC20(0xe0654C8e6fd4D733349ac7E09f6f23DA256bF475);
    IComptroller public constant Comptroller = IComptroller(0x37517C5D880c5c282437a3Da4d627B4457C10BEB);
    
    address constant public USDC = 0x04068DA6C83AFCFA0e13ba15A6696662335D5B75;
    address constant public WFTM = 0x21be370D5312f44cB42ce377BC9b8a0cEF1A4C83;

    ILocker public locker = ILocker(address(0));
    address public keeper = address(0);

    bool public disabled = false; // For emergency or farm deprecation
    bool public lock = false; // Protect investment & order

    uint public cBalance = 0;
    uint public epochIndex = 0;
    uint public newlyDeposited = 0;
    mapping(address => uint) private epochs; // Each account epoch entered
    mapping(uint => uint) private zwjkHistory; // Will keep a track of each wjk total reward
    mapping(uint => uint) private tokenHistory; // Will keep a track of his share of the pool

    address ownerAddress = address(0);

    constructor() ERC20("BIFI Bean", "bifiSOY") {
        _setupRole(DEFAULT_ADMIN_ROLE, msg.sender);
        _setupRole(CONTRACT_ROLE, msg.sender);
        ownerAddress = msg.sender;
    }

    // ------- Actions -------

    function depositAdmin(uint tokenAmount) public onlyRole(CONTRACT_ROLE) {
        // Locker & Treasury function
        _deposit(tokenAmount);
    }

    function deposit(uint tokenAmount) public {
        require(!lock, "BifSoy:Distribution is going on");
        require(tokenAmount >= 10**(_decimals-2), "BifSoy:Minimum 0.01 BIFI");
        _deposit(tokenAmount);
    }

    function _deposit(uint tokenAmount) private {
        require(!disabled, "BifSoy:Farm disabled,withdraw only");
        
        // We transfer his tokens to the smart contract, its now in its posession
        ITOKEN.safeTransferFrom(msg.sender, address(this), tokenAmount);
        
        // Is he a new depositor?
        if(balanceOf(msg.sender) == 0) {
            // Grant him the FARMER title, useful for checks to see if he is a farmer later on
            _grantRole(FARMERS, msg.sender);
            // Set his epoch to the current, personal epoch is 0 (epochs[msg.sender] - epochIndex = 0)
            epochs[msg.sender] = epochIndex;
        }else{
            // Empty his rewards, because of the architecture of the rewards.
            _withdrawRewards(balanceOf(msg.sender));
        }

        newlyDeposited += tokenAmount;
        
        _mint(msg.sender, tokenAmount); // Mint 1:1 $SOY

        emit Deposit(tokenAmount);
        // We force him to help the ecosystem
        IKeeper(keeper).distributeRewards();
    }
    
    function withdraw(uint soyAmount) public returns (uint) {
        // 7 epochs
        require(epochIndex > (epochs[msg.sender] + 7), "BifSoy:You cannot withdraw yet.");
        require(!lock, "BifSoy:Distribution is going on");
        return _withdraw(soyAmount);
    }

    function withdrawAdmin(uint soyAmount) public onlyRole(CONTRACT_ROLE) returns (uint) {
        return _withdraw(soyAmount);
    }
    
    function _withdraw(uint soyAmount) private onlyRole(FARMERS) returns (uint) {
        // He requests back more than he can
        require(balanceOf(msg.sender) >= soyAmount, "BifSoy:Insufficient amount");
        
        uint wjkAmount = 0;
        
        // Burn his sauce
        _burn(msg.sender, soyAmount);

        // If the user just deposited
        if(epochs[msg.sender] == epochIndex) {
            // Not a new depositor, its his 0 epoch, so we decrease the variable
            newlyDeposited -= soyAmount;

            // We send him his tokens later on

        }else{
            // Not a new depositor
            /*

                As he is not a new depositor, hence try to fetch his rewards
                plus, we get his cash from the revenue-generating strategy

            */

            // Decrease the participation amount
            cBalance -= soyAmount;

            // Withdraw his tokens from the IGS (interest generating strategy)
            LTOKEN.approve(address(LTOKEN), soyAmount);
            LTOKEN.redeemUnderlying(soyAmount);

            // Give him his rewards, calculated relative to the amount he withdraws
            wjkAmount += _withdrawRewards(soyAmount);
        }

        // Give him his tokens back, 1:1
        ITOKEN.safeTransfer(msg.sender, soyAmount -1); // -1 for percision cautiousness

        // Is he still one of us?
        if(balanceOf(msg.sender) == 0) _revokeRole(FARMERS, msg.sender);

        emit Withdraw(soyAmount);
        // We force him to help the entire ecosystem
        IKeeper(keeper).distributeRewards();

        return wjkAmount;
    }

    // public function, accounts can withdraw rewards without withdrawing bifi
    function withdrawRewards() public returns (uint) {
        return _withdrawRewards(balanceOf(msg.sender));
    }

    function _withdrawRewards(uint soyAmount) private returns (uint) {
        uint zwjkAmount = 0;
        uint wjkAmount = 0;
        uint cEpoch = epochs[msg.sender] + 1;

        // Avoid reentrancy;
        epochs[msg.sender] = epochIndex; // Reset his epoch count

        if(cEpoch < epochIndex)
            // Loop from the (epoch he entered + 1) to the current epoch and collect all rewards
            for(uint x = cEpoch; x <= epochIndex; x++) 
                if(zwjkHistory[x] > 0 && tokenHistory[x] > 0) 
                    zwjkAmount += (zwjkHistory[x]*10**(18 - _decimals + 18)) / ((tokenHistory[x]*10**(36 - _decimals)) / soyAmount);

        // Check if we found any rewards to send (save on gas)
        if(zwjkAmount > 0) {
            // Its always in staking mode
            wjkAmount = zwjk.unstake(zwjkAmount);
            // Transfer his WJK
            wjk.safeTransfer(msg.sender, wjkAmount);
        }

        // Useful for other contracts
        return wjkAmount;
    }

    // ------- Helpers -------

    function timeDifference(address _farmer) public view returns (uint) {
        if(hasRole(FARMERS, _farmer)) return epochIndex - epochs[_farmer];
        else return 0;
    }
    
    function checkRewards(address _farmer) public view returns (uint) {
        uint zwjkAmount = 0;
        uint soyAmount = balanceOf(_farmer);
        uint cEpoch = epochs[_farmer] + 1;
        // Some nice one line
        if(hasRole(FARMERS, _farmer) && cEpoch < epochIndex)
            for(uint x = cEpoch; x <= epochIndex; x++)
                if(zwjkHistory[x] > 0 && tokenHistory[x] > 0)
                    zwjkAmount += (zwjkHistory[x]*10**(18 - _decimals + 18)) / ((tokenHistory[x]*10**(36 - _decimals)) / soyAmount);

        return zwjkAmount;
    }

    // ------- Routine -------

    function distributeRewards() public onlyRole(CONTRACT_ROLE) {
        if(cBalance > 0) {
            lock = true;

            // Variables
            uint underlyingBalance = LTOKEN.balanceOfUnderlying(address(this));
            uint profit = 0;

            // Check if we made a profit
            if(underlyingBalance > cBalance) profit = underlyingBalance - cBalance;

            // First rounds are rough.
            if(profit > 10**(_decimals-2)) { // 0.01
                // Perform a buyback and then put into staking all the tokens

                // Get accrued revenue
                LTOKEN.redeemUnderlying(profit);

                // Only applicable to tokens that deposited into SCREAM
                Comptroller.claimComp(address(this));
                uint screamBalance = SCREAM.balanceOf(address(this));
                if(screamBalance > 1e15) { // 0.001 scream
                    uint ftmAmount = swap(address(SCREAM), WFTM, screamBalance, address(this));
                    profit += swap(WFTM, address(ITOKEN), ftmAmount, address(this));
                }

                // Tax stuff

                // 5% to locker
                uint lockerShare = profit / 20;
                ITOKEN.approve(address(locker), lockerShare);
                locker.depositToBank(address(ITOKEN), lockerShare);
                profit -= lockerShare;

                // 1% to dev
                uint devShare = profit / 100;
                ITOKEN.transfer(address(ownerAddress), devShare);
                profit -= devShare;

                // We do some swaps, because we are paired with USDC and others are with WFTM
                uint wftmAmount = swap(address(ITOKEN), WFTM, profit, address(this)); // CRV => WFTM
                uint usdcAmount = swap(WFTM, USDC, wftmAmount, address(this)); // WFTM => USDC
                uint wjkAmount = swap(USDC, address(wjk), usdcAmount, address(this)); // USDC => WJK

                // Put into leveraged staking
                wjk.approve(address(zwjk), wjkAmount);
                uint zwjkAmount = zwjk.stakeAdmin(wjkAmount);

                // We make a record of this epoch
                zwjkHistory[epochIndex] = zwjkAmount;

            // No buyback was made
            }else zwjkHistory[epochIndex] = 0;

            // We make a record of how many tokens were staked
            tokenHistory[epochIndex] = cBalance;

            // New epoch
            epochIndex++;

            // Deposit newly added tokens to contract
            /*

                This late deposit to revenue generating strategy eliminates loses on first deposit

            */
            ITOKEN.approve(address(LTOKEN), newlyDeposited);
            LTOKEN.mint(newlyDeposited); // Put to work

            cBalance += newlyDeposited;
            newlyDeposited = 0;

            lock = false;
        }
    }

    // ------- Functions -------

    // Restrict transfers of the token
    function _beforeTokenTransfer(address from, address to, uint /* amount */) internal virtual override {
        if(from != address(0) && to != address(0) && from != address(this) && to != address(this)) require(false, "!illegal");
    }

    function swap(address token0, address token1, uint amount, address to) private returns (uint) {
        address[] memory path = new address[](2);
        path[0] = token0;  
        path[1] = token1;

        IERC20(token0).approve(address(SWAP_ROUTER), amount);
        uint[] memory amounts = SWAP_ROUTER.swapExactTokensForTokens(amount, 0, path, to, block.timestamp);
        return amounts[amounts.length - 1];
    }

    // ------- Administration -------

    function setAddressToken(address newAddress) public onlyRole(DEFAULT_ADMIN_ROLE) {
        wjk = IERC20(newAddress);
    }

    function setAddressStaking(address newAddress) public onlyRole(DEFAULT_ADMIN_ROLE) {
        zwjk = IStaking(newAddress);
    }

    function setAddressLocker(address newAddress) public onlyRole(DEFAULT_ADMIN_ROLE) {
        locker = ILocker(newAddress);
    }

    function setAddressKeeper(address newAddress) public onlyRole(DEFAULT_ADMIN_ROLE) {
        keeper = newAddress;
    }
    
    function disableFarm() public onlyRole(DEFAULT_ADMIN_ROLE) {
        disabled = !disabled;
    }

    function decimals() public view virtual override returns (uint8) {
        return _decimals;
    }
}

interface ILending is IERC20 {
    function mint(uint) external returns (uint);
    function redeemUnderlying(uint) external returns (uint);
    function balanceOfUnderlying(address) external returns (uint);
}

interface ILocker {
    function depositToBank(address addr, uint amount) external;
}

interface IComptroller {
    function claimComp(address holder) external;
    function balanceOf(address) external view returns(uint);
}

interface IStaking is IERC20 {
    function stakeAdmin(uint) external returns(uint);
    function unstake(uint) external returns(uint);
}

interface IKeeper {
    function distributeRewards() external;
}